plugins {
    id "java"
    id "net.minecrell.licenser" version "0.4.1"
    id "maven-publish"
}

group "dev.architectury"
archivesBaseName = rootProject.name
version = "1.0." + (System.getenv("GITHUB_RUN_NUMBER") == null ? "9999" : System.getenv("GITHUB_RUN_NUMBER"))
sourceCompatibility = targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.jetbrains:annotations:20.1.0"
}

license {
    include "**/*.java"
    header = project.file("HEADER.txt")
    ext {
        name = "architectury"
        year = "2021"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
    repositories {
        if (System.getenv("MAVEN_PASS") != null) {
            maven {
                url = "https://deploy.shedaniel.me/"
                credentials {
                    username = "shedaniel"
                    password = System.getenv("MAVEN_PASS")
                }
            }
        }
    }
}
